plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "io.freefair.lombok" version "6.5.0.2"
    id 'java'
    id 'jacoco'
    id 'org.barfuin.gradle.jacocolog' version '2.0.0'
    id 'application'
}

group 'org.bufistov'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    // https://mvnrepository.com/artifact/com.datastax.cassandra/cassandra-driver-core
    implementation 'com.datastax.cassandra:cassandra-driver-core:3.11.2'
    implementation 'com.datastax.cassandra:cassandra-driver-mapping:3.11.2'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'com.google.guava:guava:31.1-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.cassandraunit:cassandra-unit:4.3.1.0'
    testImplementation 'org.mockito:mockito-core:4.6.1'
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events "started", "skipped", "failed"
        exceptionFormat "short"

        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'org/bufistov/model/**')
        }))
    }
}

testing {
    suites {
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project
                implementation 'org.apache.logging.log4j:log4j-api:2.18.0'
                implementation 'org.apache.logging.log4j:log4j-core:2.18.0'
                implementation 'com.datastax.cassandra:cassandra-driver-core:3.11.2'
                implementation 'com.datastax.cassandra:cassandra-driver-mapping:3.11.2'
                implementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
                runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
                implementation 'org.testcontainers:junit-jupiter:1.17.3'
                implementation 'org.testcontainers:cassandra:1.17.3'
                implementation 'org.hamcrest:hamcrest:2.2'
                implementation 'org.awaitility:awaitility:4.2.0'
            }
        }
    }
}

application {
    mainClass = 'org.bufistov.QueryPopulator'
}
